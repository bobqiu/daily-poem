class AP.Router extends Backbone.Router
  routes:
    "*path": 'glob'

  open: (path...) ->
    @navigate path.join("/"), trigger: true

  update: (path...) ->
    @navigate path.join("/")

  glob: (path) ->
    [action, id, other...] = (path ? '').split('/')
    action ||= 'main'

    if @[action]
      console.debug "open #{action}(#{id ? ''})"
      @[action](id, other...)
    else
      console.warn "no route for #{path}"
      @navigate '', trigger: true

  main:          -> @openView 'Main', Model.date
  today:         -> @openView 'Main', Model.date
  tomorrow:      -> @openView 'Main', Model.date.last().next()
  poems: (id)    -> @openView 'Main', id
  calendar: (id) -> @openView 'Calendar'
  favorites:     -> @openView 'Favorites'
  about:         -> @openView 'About'
  developer:     -> @openView 'DeveloperMenu'

  openView: (viewName, args...) ->
    @currentView?.unmount()
    @currentView = new AP["#{viewName}View"](args...)
    @currentView.show()
    null

  start: ->
    Backbone.history.start()

#
# if @constructor.disabled
#   console.warn "router disabled"
#   return
#
# ... return if $(link).hasClass("external")
#
# $(document).on 'click', 'a.back.link', (e) =>
#   @go ''
#
