class AP.Application
  constructor: ->
    @sidebar = new AP.SidebarView
    @sidebar.render()

    @f7app = new Framework7 dynamicPageUrl: 'page-{{name}}', pjax: true, ajaxLinks: 'ajax', material: APP_THEME_NAME is 'material'
    @f7view = @f7app.addView '.view-main', dynamicNavbar: true
    @f7router = @f7view.router

    navigator.splashscreen?.hide()
    setTimeoutTo 500, => StatusBar?.show()

    Model.load @loaded

    @setupGlobalEvents()

  loaded: =>
    @router = new AP.Router
    @router.start()

    @notifications = new AP.Notifications
    @clickManager = new AP.ClickManager
    @deviceInfo = new AP.DeviceInfo
    @lifecycle = new AP.Lifecycle
    @sharingManager = new AP.SharingManager

    if @screenshotsMode
      @screenshots = new AP.Screenshots
      @screenshots.enable()
      @screenshots.performNextTestAction()

  setupGlobalEvents: ->
    $(document).on 'click', 'a.slide', (e) ->
      e.preventDefault()
      app.router.slide @getAttribute('href')

    $(document).on 'click', '.navbar .link.back', (e) ->
      history.back()

    $(document).on 'click', "a.link[href='#']", (e) ->
      e.preventDefault()
